buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
            changing = true
        }
    }
}

apply plugin: 'forge'

version = "1.1.0"
group = "com.pyding.deathlyhallows"
archivesBaseName = "deathlyhallows"

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'


minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
}

configurations {
	provided
	included
}

sourceSets {
    main {
		compileClasspath += configurations.provided
		compileClasspath += configurations.included
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

dependencies {
	included project(':ForgeASMTreeEditLib')
	provided fileTree(dir: "providedLibs", include: "*.jar")
}

jar {
	manifest {
		attributes 'FMLCorePlugin': 'com.pyding.deathlyhallows.core.DHCore'
		attributes 'FMLCorePluginContainsFMLMod': 'true'
		attributes 'FMLAT': 'dh_at.cfg'
	}
	from {
		configurations.included.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}
